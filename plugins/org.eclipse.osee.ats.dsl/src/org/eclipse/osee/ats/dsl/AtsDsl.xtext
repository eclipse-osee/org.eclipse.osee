grammar org.eclipse.osee.ats.dsl.AtsDsl with org.eclipse.xtext.common.Terminals

generate atsDsl "http://www.eclipse.org/osee/ats/dsl/AtsDsl"

AtsDsl:
	(('userDefinition' userDef+=UserDef)*
	('teamDefinition' teamDef+=TeamDef)*
	('actionableItem' actionableItemDef+=ActionableItemDef)*
	('workDefinition' workDef+=WorkDef)*
	('program' program+=ProgramDef)*) | ('rule' rule+=Rule)*;

USER_DEF_REFERENCE:
	STRING;
	
UserDef:
	name=USER_DEF_REFERENCE (userDefOption+=UserDefOption)*
	('{'
		('active' active=BooleanDef)?
		('userId' userId=STRING)?
		('email' email=STRING)?
		('isAdmin' admin=BooleanDef)?
	'}')?;

ATTR_DEF_REFERENCE:
	STRING;
	
AttrDef:
	name=ATTR_DEF_REFERENCE option=AttrDefOptions;

AttrDefOptions:
	AttrValueDef | AttrFullDef;
	
AttrValueDef:
   'value' value=STRING;
   
AttrFullDef:
	'{'
		('id' uuid=STRING)?
		('value' values+=STRING)+
	'}';

enum OnEventType:
	CreateBranch | CommitBranch | CreateWorkflow | TransitionTo | Manual;
	
PROGRAM_REFERENCE:
	STRING;
	
ProgramDef:
	name=PROGRAM_REFERENCE (programDefOption+=ProgramDefOption)*
	'{'
		('id' uuid=INT)?
		('artifactType' artifactTypeName=STRING)?
		('active' active=BooleanDef)?
		('namespace' namespace=STRING)*
		('teamDefinition' teamDefinition=TEAM_DEF_REFERENCE)*
		('attribute' attributes+=AttrDef)*
	'}';

TEAM_DEF_REFERENCE:
	STRING;
	
TeamDef:
	name=TEAM_DEF_REFERENCE (teamDefOption+=TeamDefOption)*
	'{'
		('id' uuid=INT)?
		('active' active=BooleanDef)?
		('staticId' staticId+=STRING)*
		('lead' lead+=UserRef)*
		('member' member+=UserRef)*
		('privileged' privileged+=UserRef)*
		('workDefinition' workDefinition=STRING)?
		('relatedTaskWorkDefinition' relatedTaskWorkDefinition=STRING)?
		('teamWorkflowArtifactType' teamWorkflowArtifactType=STRING)?
		('accessContextId' accessContextId+=STRING)*
		('version' version+=VersionDef)*
	   ('rule' rules+=RuleRef)*
		('children' '{'
		   ('teamDefinition' children+=TeamDef)+
		   '}'
		 )?
	'}';

AI_DEF_REFERENCE:
	STRING;

ActionableItemDef:
	name=AI_DEF_REFERENCE (aiDefOption+=ActionableItemOption)*
	('{'
		('id' uuid=INT)?
		('active' active=BooleanDef)?
		('actionable' actionable=BooleanDef)?
		('lead' lead+=UserRef)*
		('owner' owner+=UserRef)*
		('staticId' staticId+=STRING)*
		('team' teamDef=STRING)?
		('accessContextId' accessContextId+=STRING)*
	   ('rule' rules+=RuleRef)*
		('children' '{'
		   ('actionableItem' children+=ActionableItemDef)+
		   '}'
		 )?
	'}')?;

VERSION_DEF_REFERENCE:
	STRING;

VersionDef:
	name=VERSION_DEF_REFERENCE
	'{'
		('id' uuid=INT)?
		('staticId' staticId+=STRING)*
		('next' next=BooleanDef)?
		('released' released=BooleanDef)?
		('allowCreateBranch' allowCreateBranch=BooleanDef)?
		('allowCommitBranch' allowCommitBranch=BooleanDef)?
		('baselineBranchUuid' baselineBranchUuid=STRING)?
		('parallelVersion' parallelVersion+=STRING)*
	'}';

WorkDef:
	name=WORK_DEFINITION_NAME_REFERENCE
	'{'
	('id' id+=STRING)+
	('startState' startState=[StateDef|STATE_NAME_REFERENCE])
	(widgetDefs+=WidgetDef)*
	(decisionReviewDefs+=DecisionReviewDef)*
	(peerReviewDefs+=PeerReviewDef)*
	(states+=StateDef)+
	'}';

WidgetDef:
	'widgetDefinition' name=WIDGET_NAME_REFERENCE
	'{'
	('attributeName' attributeName=STRING)?
	('description' description=STRING)?
	('xWidgetName' xWidgetName=STRING)?
	('defaultValue' defaultValue=STRING)?
	('height' height=INT)?
	('option' option+=WidgetOption)*
	('minConstraint' minConstraint=STRING)?
	('maxConstraint' maxConstraint=STRING)?
	'}';

WidgetRef:
	'widget' widget=[WidgetDef|WIDGET_NAME_REFERENCE];

AttrWidget:
	'attributeWidget' attributeName=STRING ('with' option+=WidgetOption)*;

StateDef:
	'state' name=STATE_NAME_REFERENCE
	'{'
	('description' description=STRING)?
	'type' pageType=PageType
	'ordinal' ordinal=INT
	(transitionStates+=ToState)*
	('rule' rules+=RuleRef)*
	(decisionReviews+=DecisionReviewRef)*
	(peerReviews+=PeerReviewRef)*
	('percentWeight' percentWeight=INT)?
	('recommendedPercentComplete' recommendedPercentComplete=INT)?
	('color' color=StateColor)?
	(layout=LayoutType)?
	'}';

DecisionReviewRef:
	'decisionReview' decisionReview=[DecisionReviewDef|DECISION_REVIEW_NAME_REFERENCE];

DecisionReviewDef:
	'decisionReviewDefinition' name=DECISION_REVIEW_NAME_REFERENCE
	'{'
	'title' title=STRING
	'description' description=STRING
	('relatedToState' relatedToState=[StateDef|STATE_NAME_REFERENCE])?
	'blockingType' blockingType=ReviewBlockingType
	'onEvent' stateEvent=WorkflowEventType
	('assignee' assigneeRefs+=UserRef)*
	('autoTransitionToDecision' autoTransitionToDecision=BooleanDef)?
	(options+=DecisionReviewOpt)+
	'}';

DECISION_REVIEW_OPT_REF:
	STRING;
	
DecisionReviewOpt:
	'option' name=DECISION_REVIEW_OPT_REF (followup=FollowupRef)?;

PeerReviewRef:
	'peerReview' peerReview=[PeerReviewDef|PEER_REVIEW_NAME_REFERENCE];

PeerReviewDef:
	'peerReviewDefinition' name=PEER_REVIEW_NAME_REFERENCE
	'{'
	('title' title=STRING)?
	'description' description=STRING
	('location' location=STRING)?
	('relatedToState' relatedToState=[StateDef|STATE_NAME_REFERENCE])?
	'blockingType' blockingType=ReviewBlockingType
	'onEvent' stateEvent=WorkflowEventType
	('assignee' assigneeRefs+=UserRef)*
	'}';

FollowupRef:
	'followup by' ('assignee' assigneeRefs+=UserRef)+;

UserRef:
	UserByUserId | UserByName;

UserByUserId:
	'userId' userId=STRING;

UserByName:
	'named' userName=STRING;

enum BooleanDef:
	None | True | False;

enum WorkflowEventType:
	TransitionTo | CreateBranch | CommitBranch;

enum ReviewBlockingType:
	Transition | Commit;

DECISION_REVIEW_NAME_REFERENCE:
	STRING;

PEER_REVIEW_NAME_REFERENCE:
	STRING;

STATE_NAME_REFERENCE:
	STRING;

WIDGET_NAME_REFERENCE:
	STRING;

WORK_DEFINITION_NAME_REFERENCE:
	STRING;
	
ToState:
	'to' state=[StateDef|STATE_NAME_REFERENCE] (options+=TransitionOption)*;

LayoutType:
	LayoutDef | LayoutCopy;

LayoutDef:
	'layout' '{' (layoutItems+=LayoutItem)+ '}';

LayoutCopy:
	'layoutCopyFrom' state=[StateDef|STATE_NAME_REFERENCE];

LayoutItem:
	WidgetRef | AttrWidget | Composite;

Composite:
	'composite' '{'
	'numColumns' numColumns=INT
	(layoutItems+=LayoutItem)+
	('option' options+=CompositeOption)*
	'}';

USER_DEF_OPTION_NAME:
	STRING;
	
UserDefOption:
	'GetOrCreate' | USER_DEF_OPTION_NAME;
	
PROGRAM_DEF_OPTION_NAME:
	STRING;
	
ProgramDefOption:
	'GetOrCreate' | PROGRAM_DEF_OPTION_NAME;
	
TEAM_DEF_OPTION_NAME:
	STRING;
	
TeamDefOption:
	'GetOrCreate' | TEAM_DEF_OPTION_NAME;
	
AI_DEF_OPTION_NAME:
	STRING;
	
ActionableItemOption:
	'GetOrCreate' | AI_DEF_OPTION_NAME;

// STRING option used below for future expansion of options.  Since sheet will be
// in databse and read by old code, new value can be entered as "NewOption" with quotes.
// Old grammar and code will ignore; New code needs to process string and resolve to new
// value.  After release, instance sheets can be updated and remove the " because NewOption
// will be a supported value.

COMPOSITE_OPTION_NAME:
	STRING;
	
// STRING is for future expansion; Composite has no options, provide this block so some
// can be added later
CompositeOption:
	'None' | COMPOSITE_OPTION_NAME;

TRANSITION_OPTION_NAME:
	STRING;
	
	// STRING is for future expansion
// WARNING: Any new TransitionOption need to be added to AtsDslProposalProvider for content assist
TransitionOption:
	'AsDefault' | 'OverrideAttributeValidation' | TRANSITION_OPTION_NAME;


RULE_NAME_REFERENCE:
	STRING;
	
enum RuleLocation:
	StateDefinition | TeamDefinition | ActionableItem;
	
	// STRING is for future expansion
	
RuleDef returns RuleDef:
	('name' name=RULE_NAME_REFERENCE)
	'{'
	('title' title=STRING)
	('description' description=STRING)?
	('ruleLocation' ruleLocation+=RuleLocation)+
	 '}';
	
CreateTaskRuleDef returns CreateTaskRule:
	('name' name=RULE_NAME_REFERENCE)
	'{'
	('title' title=STRING)
	('description' description=STRING)?
	('ruleLocation' ruleLocation+=RuleLocation)+
	('assignees' assignees+=UserDef)*
	('relatedState' relatedState=STATE_NAME_REFERENCE)?
	('taskWorkDef' taskWorkDef=STRING)?
	('onEvent' onEvent+=OnEventType)*
	('attribute' attributes+=AttrDef)*
	'}';

CreateDecisionReviewRuleDef returns CreateDecisionReviewRule:
	('name' name=RULE_NAME_REFERENCE)
	'{'
	('title' title=STRING)
	('description' description=STRING)?
	('ruleLocation' ruleLocation+=RuleLocation)+
	('assignees' assignees+=UserDef)*
	('relatedToState' relatedToState=STATE_NAME_REFERENCE)?
	'blockingType' blockingType=ReviewBlockingType
	'onEvent' stateEvent=WorkflowEventType
	('autoTransitionToDecision' autoTransitionToDecision=BooleanDef)
	(options+=DecisionReviewOpt)+
	('attribute' attributes+=AttrDef)*
   '}';
	
CreatePeerReviewRuleDef returns CreatePeerReviewRule:
	('name' name=RULE_NAME_REFERENCE)
	'{'
	('title' title=STRING)
	('description' description=STRING)?
	('ruleLocation' ruleLocation+=RuleLocation)+
	('assignees' assignees+=UserDef)*
	('relatedToState' relatedToState=STATE_NAME_REFERENCE)?
	('location' location=STRING)?
	'blockingType' blockingType=ReviewBlockingType
	'onEvent' stateEvent=WorkflowEventType
	('attribute' attributes+=AttrDef)*
   '}';
	
// WARNING: Any new rules need to be added to AtsDslProposalProvider for content assist
RuleRef:
	'RequireStateHourSpentPrompt' |
	'AddDecisionValidateBlockingReview' |
	'AddDecisionValidateNonBlockingReview' |
	'AllowTransitionWithWorkingBranch' |
	'ForceAssigneesToTeamLeads' |
	'RequireTargetedVersion' |
	'AllowPrivilegedEditToTeamMember' |
	'AllowPrivilegedEditToTeamMemberAndOriginator' |
	'AllowPrivilegedEditToAll' |
	'AllowEditToAll' |
	'AllowAssigneeToAll' |
	'AllowTransitionWithoutTaskCompletion' |
  	RULE_NAME_REFERENCE;

ReviewRule:
	(CreateDecisionReviewRuleDef | CreatePeerReviewRuleDef)
;

Rule:
	( RuleDef | CreateTaskRuleDef | ReviewRule);

//RuleRef:
//	( Rule | bool?=Boolean_Rule); //(cardinality=('?'|'+'|'*'));

WIDGET_OPTION_NAME:
	STRING;
	
	// STRING is for future expansion
// WARNING: Any new WidgetOption need to be added to AtsDslProposalProvider for content assist
WidgetOption:
	'REQUIRED_FOR_TRANSITION' | 'NOT_REQUIRED_FOR_TRANSITION' |
	'REQUIRED_FOR_COMPLETION' | 'NOT_REQUIRED_FOR_COMPLETION' |
	'ENABLED' | 'NOT_ENABLED' |
	'EDITABLE' | 'NOT_EDITABLE' |
	'FUTURE_DATE_REQUIRED' | 'NOT_FUTURE_DATE_REQUIRED' |
	'MULTI_SELECT' |
	'HORIZONTAL_LABEL' | 'VERTICAL_LABEL' |
	'LABEL_AFTER' | 'LABEL_BEFORE' | 'NO_LABEL' |
	'SORTED' |
	'ADD_DEFAULT_VALUE' | 'NO_DEFAULT_VALUE' |
	'BEGIN_COMPOSITE_4' | 'BEGIN_COMPOSITE_6' | 'BEGIN_COMPOSITE_8' | 'BEGIN_COMPOSITE_10' |
	'END_COMPOSITE' |
	'FILL_NONE' | 'FILL_HORIZONTALLY' | 'FILL_VERTICALLY' |
	'ALIGN_LEFT' | 'ALIGN_RIGHT' | 'ALIGN_CENTER' | WIDGET_OPTION_NAME;

PAGE_TYPE_NAME:
	STRING;
	
	// STRING is for future expansion
// WARNING: Any new PageType need to be added to AtsDslProposalProvider for content assist
PageType:
	'Working' | 'Completed' | 'Cancelled' | PAGE_TYPE_NAME;

COLOR_NAME:
	STRING;

// STRING is for future expansion
// WARNING: Any new color needs to be added to StateColor; Must be an SWT.COLOR color
StateColor:
	'BLACK' | 'WHITE' | 'RED' | 'DARK_RED' | 'GREEN' | 'DARK_GREEN' | 'YELLOW' | 'DARK_YELLOW' | 'BLUE' | 'DARK_BLUE' | 'MAGENTA'
	 | 'DARK_MAGENTA' | 'CYAN' | 'DARK_CYAN' | 'GRAY' | 'DARK_GRAY' | COLOR_NAME ;

	 
	 
