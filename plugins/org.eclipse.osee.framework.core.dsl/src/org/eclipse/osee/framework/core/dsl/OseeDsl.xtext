grammar org.eclipse.osee.framework.core.dsl.OseeDsl with org.eclipse.xtext.common.Terminals

generate oseeDsl "http://www.eclipse.org/osee/framework/core/dsl/OseeDsl"

OseeDsl:
	(imports+=Import)*
	(artifactTypes+=XArtifactType | relationTypes+=XRelationType | attributeTypes+=XAttributeType |
	enumTypes+=XOseeEnumType | enumOverrides+=XOseeEnumOverride | artifactTypeOverrides+=XOseeArtifactTypeOverride)*
	(artifactMatchRefs+=XArtifactMatcher |
	accessDeclarations+=AccessContext | roleDeclarations+=Role)*;

Import:
	'import' importURI=STRING;

terminal STRING:
	'"' ('\\' ('"') | !('"'))* '"' | "'" ('\\' ("'") | !("'"))* "'";

terminal WHOLE_NUM_STR:
	('0'..'9')+;

QUALIFIED_NAME:
	ID ('.' ID)*;

	///////////////////////////////////////
//  OSEE TYPE MODEL                  //
///////////////////////////////////////
OseeElement:
	XOseeEnumOverride | OseeType;

OseeType:
	XArtifactType | XRelationType | XAttributeType | XOseeEnumType;

XArtifactType:
	(abstract?='abstract')? 'artifactType' name=STRING ('extends'
	superArtifactTypes+=[XArtifactType|STRING] (','
	superArtifactTypes+=[XArtifactType|STRING])*)? '{'
	'id' id=WHOLE_NUM_STR
	(validAttributeTypes+=XAttributeTypeRef)* '}';

XAttributeTypeRef:
	'attribute' validAttributeType=[XAttributeType|STRING] ('branchUuid' branchUuid=WHOLE_NUM_STR)?;

XAttributeType:
	'attributeType' name=STRING ('extends' baseAttributeType=AttributeBaseType) ('overrides'
	override=[XAttributeType|STRING])? '{' 'id' id=WHOLE_NUM_STR 'dataProvider'
	dataProvider=('DefaultAttributeDataProvider' | 'UriAttributeDataProvider' | QUALIFIED_NAME) 'min' min=WHOLE_NUM_STR
	'max' max=(WHOLE_NUM_STR | 'unlimited') (('taggerId' taggerId=('DefaultAttributeTaggerProvider' | QUALIFIED_NAME))? &
	('enumType' enumType=[XOseeEnumType|STRING])? & ('description' description=STRING)? & ('defaultValue'
	defaultValue=STRING)? & ('fileExtension' fileExtension=STRING)? & ('mediaType' mediaType=STRING)?) '}';

AttributeBaseType:
	'BooleanAttribute' | 'CompressedContentAttribute' | 'DateAttribute' | 'EnumeratedAttribute' |
	'FloatingPointAttribute' | 'IntegerAttribute' | 'LongAttribute' | 'JavaObjectAttribute' | 'StringAttribute' |
	'ArtifactReferenceAttribute' | 'BranchReferenceAttribute' | 'WordAttribute' | 'OutlineNumberAttribute' |
	QUALIFIED_NAME;

XOseeEnumType:
	'oseeEnumType' name=STRING '{' 'id' id=WHOLE_NUM_STR (enumEntries+=XOseeEnumEntry)* '}';

XOseeEnumEntry:
	'entry' name=STRING (ordinal=WHOLE_NUM_STR)? ('description' description=STRING)?;

XOseeEnumOverride:
	'overrides enum' overridenEnumType=[XOseeEnumType|STRING] '{' (inheritAll?='inheritAll')?
	(overrideOptions+=OverrideOption)* '}';

OverrideOption:
	AddEnum | RemoveEnum;

AddEnum:
	'add' enumEntry=STRING (ordinal=WHOLE_NUM_STR)? ('description' description=STRING)?;

RemoveEnum:
	'remove' enumEntry=[XOseeEnumEntry|STRING];

XOseeArtifactTypeOverride:
	'overrides artifactType' overridenArtifactType=[XArtifactType|STRING] '{'
	(inheritAll?='inheritAll')?
	(overrideOptions+=AttributeOverrideOption)+
	'}';

AttributeOverrideOption:
	AddAttribute | RemoveAttribute | UpdateAttribute;

AddAttribute:
	'add' attribute=XAttributeTypeRef;

RemoveAttribute:
	'remove' 'attribute' attribute=[XAttributeType|STRING];

UpdateAttribute:
	'update' attribute=XAttributeTypeRef;

XRelationType:
	'relationType' name=STRING '{' 'id' id=WHOLE_NUM_STR
	'sideAName' sideAName=STRING 'sideAArtifactType' sideAArtifactType=[XArtifactType|STRING]
	'sideBName' sideBName=STRING 'sideBArtifactType' sideBArtifactType=[XArtifactType|STRING]
	'defaultOrderType' defaultOrderType=RelationOrderType 'multiplicity' multiplicity=RelationMultiplicityEnum '}';

enum RelationMultiplicityEnum:
	ONE_TO_ONE | ONE_TO_MANY | MANY_TO_ONE | MANY_TO_MANY;

RelationOrderType:
	'Lexicographical_Ascending' | 'Lexicographical_Descending' | 'Unordered' | ID;

	///////////////////////////////////////
//  OSEE MATCH RULES //
///////////////////////////////////////
enum CompareOp:
	EQ | LIKE;

enum XLogicOperator:
	AND | OR;

CONDITION_VALUE:
	WHOLE_NUM_STR | STRING;

enum MatchField:
	artifactName | artifactId | branchName | branchUuid;

Condition:
	SimpleCondition | CompoundCondition;

SimpleCondition:
	field=MatchField op=CompareOp expression=STRING;

CompoundCondition:
	'(' conditions+=SimpleCondition (operators+=XLogicOperator conditions+=SimpleCondition)+ ')';

XArtifactMatcher:
	'artifactMatcher' name=STRING 'where'
	conditions+=Condition (operators+=XLogicOperator conditions+=Condition)* ';';

	///////////////////////////////////////
//  OSEE ACCESS MODEL                //
///////////////////////////////////////
Role:
	'role' name=STRING ('extends' superRoles+=[Role|STRING])? '{'
	(usersAndGroups+=UsersAndGroups | referencedContexts+=ReferencedContext)+ '}';

ReferencedContext:
	'accessContext' accessContextRef=STRING ';';

UsersAndGroups:
	'id' userOrGroupId=WHOLE_NUM_STR ';';

AccessContext:
	'accessContext' name=STRING ('extends'
	superAccessContexts+=[AccessContext|STRING])? '{' 'id' id=WHOLE_NUM_STR ';'
	(accessRules+=ObjectRestriction | hierarchyRestrictions+=HierarchyRestriction)+ '}';

HierarchyRestriction:
	'childrenOf' artifactMatcherRef=[XArtifactMatcher|STRING] '{' (accessRules+=ObjectRestriction)+
	'}';

enum AccessPermissionEnum:
	ALLOW | DENY;

RelationTypeArtifactTypePredicate:
	'artifactType' artifactTypeRef=[XArtifactType|STRING];

RelationTypeArtifactPredicate:
	'artifact' artifactMatcherRef=[XArtifactMatcher|STRING];

RelationTypePredicate:
	RelationTypeArtifactPredicate | RelationTypeArtifactTypePredicate;

ObjectRestriction:
	ArtifactMatchRestriction | ArtifactTypeRestriction | RelationTypeRestriction | AttributeTypeRestriction;

ArtifactMatchRestriction:
	permission=AccessPermissionEnum 'edit' 'artifact' artifactMatcherRef=[XArtifactMatcher|STRING]
	';';

ArtifactTypeRestriction:
	permission=AccessPermissionEnum 'edit' 'artifactType' artifactTypeRef=[XArtifactType|STRING] ';';

AttributeTypeRestriction:
	permission=AccessPermissionEnum 'edit' 'attributeType' attributeTypeRef=[XAttributeType|STRING]
	('of' 'artifactType' artifactTypeRef=[XArtifactType|STRING])? ';';

LegacyRelationTypeRestriction:
	permission=AccessPermissionEnum 'edit' 'relationType' relationTypeRef=[XRelationType|STRING]
	restrictedToSide=XRelationSideEnum ('artifact' artifactMatcherRef=[XArtifactMatcher|STRING])? ';';

RelationTypeRestriction:
	permission=AccessPermissionEnum 'edit' 'relationType' (relationTypeMatch?=RelationTypeMatch |
	relationTypeRef=[XRelationType|STRING])
	restrictedToSide=XRelationSideEnum (predicate=RelationTypePredicate)? ';';

enum RelationTypeMatch:
	ALL;

enum XRelationSideEnum:
	SIDE_A | SIDE_B | BOTH;
	